// Copyright 2024 The Terasology Foundation
// SPDX-License-Identifier: Apache-2.0

plugins {
    id 'java-library'
}

group 'io.github.benjaminamos.TracyJavaBindings'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

sourceSets {
    main
}

tasks.withType(Jar).all { jarTask ->
    def sharedLibraryTasks = project('tracy-jni').tasks.withType(LinkSharedLibrary)
    jarTask.dependsOn(sharedLibraryTasks)

    def sharedLibraryOutputs = sharedLibraryTasks.find { task ->
        task.name.toLowerCase().contains("release")
    }?.outputs?.files?.asFileTree?.files ?: []

    jarTask.from(sharedLibraryOutputs) {
        include "*.dll"
        into "windows"
    }
    jarTask.from(sharedLibraryOutputs) {
        include "*.so"
        into "linux"
    }
    jarTask.from(sharedLibraryOutputs) {
        include "*.dylib"
        into "macosx"
    }

    jarTask.from(new File(project('tracy-jni').projectDir, "tracy")) {
        include "LICENSE"
        rename "LICENSE", "TRACY_LICENSE"
    }
}

tasks.named("test").configure { Test testTask ->
    testTask.useJUnitPlatform()
 
    testTask.maxHeapSize = "1G"
 
    testTask.testLogging {
        events "passed"
    }
 
    testTask.systemProperty(
            "java.library.path",
            project(":tracy-jni")
                    .layout
                    .buildDirectory
                    .dir("lib/main/debug")
                    .get()
                    .asFile
                    .absolutePath
    )
}
